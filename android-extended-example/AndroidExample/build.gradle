apply plugin: 'com.android.application'
apply from: rootProject.file('gradle/signing.gradle')
apply plugin: 'android-checkstyle'
apply plugin: 'android-pmd'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        versionCode 1
        versionName '1.0'
        minSdkVersion 7
        targetSdkVersion 21
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            buildConfigField "boolean", "SHOW_GREETINGS", "Boolean.parseBoolean(\"true\")"
            buildConfigField "String", "GREETINGS", "\"Greetings, My Lord !!!\""
        }
        release {
            signingConfig signingConfigs.releaseKeystore
            buildConfigField "boolean", "SHOW_GREETINGS", "Boolean.parseBoolean(\"false\")"
            buildConfigField "String", "GREETINGS", "\"Hello, World !!!\""
        }
    }

    lintOptions {
        abortOnError false
        checkAllWarnings true
        // for most projects accessibility tools support is out of scope
        disable 'ContentDescription'
        // a lot of Java libraries have references to Java SE packages
        disable 'InvalidPackage'
        // sp can be scaled by the user's font size preference so can break design
        disable 'SpUsage'
        // there are no pixel art for the most projects
        disable 'IconMissingDensityFolder'
        // it is just reminder
        disable 'SelectableText'
        // there are too few projects with RTL support
        disable 'RtlCompat', 'RtlEnabled', 'RtlHardcoded', 'RtlSymmetry'
    }
}

dependencies {
    compile fileTree(dir: rootProject.file('libs'), include: ['*.jar'])
    compile fileTree(dir: project.file('libs'), include: ['*.jar'])
    compile project(':AndroidLibrary')
    compile 'com.android.support:support-v4:21.0.2'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'com.github.tony19:logback-android-classic:1.1.1-3'
}

androidCheckstyle {
    abortOnError false
    config rootProject.file('gradle/config/checkstyle.xml')
}

androidPmd {
    abortOnError false
    config rootProject.file('gradle/config/pmd.xml')
}
